plugins {
    id "com.jfrog.bintray" version "1.8.3"
}

subprojects {
    apply plugin: 'java'
    apply plugin: 'maven'
    apply plugin: 'maven-publish'
    apply plugin: 'com.jfrog.bintray'

    group 'com.github.vmg.protogen'

    sourceCompatibility = 1.8
    targetCompatibility = 1.8

    repositories {
        jcenter()
    }

    jar {
        manifest {
            attributes('Implementation-Title': project.name,
                    'Implementation-Version': project.version)
        }
    }

    task sourceJar(type: Jar) {
        from sourceSets.main.allJava
    }

    bintray {
        user = project.hasProperty('bintrayUser') ? project.property('bintrayUser') : System.getenv('BINTRAY_USER')
        key = project.hasProperty('bintrayApiKey') ? project.property('bintrayApiKey') : System.getenv('BINTRAY_API_KEY')

        publish = true

        publications = ['PubBintray']

        pkg {
            repo = 'protogen'
            name = project.name
            userOrg = user
            licenses = ['Apache-2.0']
            vcsUrl = 'https://github.com/vmg/protogen'
        }
    }

    publishing {
        publications {
            PubBintray(MavenPublication) {
                from components.java
                artifact sourceJar {
                    classifier "sources"
                }
            }
        }
    }
}

